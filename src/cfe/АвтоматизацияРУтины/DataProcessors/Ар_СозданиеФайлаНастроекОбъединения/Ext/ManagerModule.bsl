
Функция СформироватьФайлНастроекПоДанным(Данные) Экспорт
	
	Отказ 		= Ложь;
	Результат 	= Неопределено;
	
	ПроверитьВозможностьФормированияФайлаПоДанным(Данные, Отказ);
	
	Если Не Отказ Тогда
		
		МассивИдентификаторовОбъектовМетаданных = Новый Массив;
		
		Если ТипЗнч(Данные.Источник) = Тип("ДокументСсылка.узВыпускРелиза") Тогда
			МассивИдентификаторовОбъектовМетаданных = ПолучитьМассивИдентификаторовОбъектовМетаданныхПоВыпускуРелиза(Данные.Источник);
		ИначеЕсли ТипЗнч(Данные.Источник) = Тип("Массив") Тогда
			МассивИдентификаторовОбъектовМетаданных = Данные.Источник;
		КонецЕсли;
		
		Результат = СформироватьФайлНастроекПоМассивуИдентификаторовОбъектовМетаданных(МассивИдентификаторовОбъектовМетаданных);
		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

#Область ФормированиеФайла

Функция СформироватьФайлНастроекПоМассивуИдентификаторовОбъектовМетаданных(Массив)
	
	ИмяВременногоФайла 							= ПолучитьИмяВременногоФайла("xml");
	СоответсвиеДанныхИдентификаторовМетаданных 	= ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Массив, "Родитель, ИмяМетаданных, ПолноеИмяМетаданных");
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла, "UTF-8");
	
	//Settings
	ЗаписьXML.ЗаписатьНачалоЭлемента("Settings");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("", "http://v8.1c.ru/8.3/config/merge/settings");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xs", "http://www.w3.org/2001/XMLSchema");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьАтрибут("version", "1.1");
	
	//Parameters
	ЗаписьXML.ЗаписатьНачалоЭлемента("Parameters");
	
	//ConfigurationsRelation
	ЗаписьXML.ЗаписатьНачалоЭлемента("ConfigurationsRelation");
	ЗаписьXML.ЗаписатьТекст("SecondConfigurationIsDescendantOfMainConfiguration");
	ЗаписьXML.ЗаписатьКонецЭлемента(); //ConfigurationsRelation	
	
	//AllowMainConfigurationObjectDeletion
	ЗаписьXML.ЗаписатьНачалоЭлемента("AllowMainConfigurationObjectDeletion");
	ЗаписьXML.ЗаписатьТекст("true");
	ЗаписьXML.ЗаписатьКонецЭлемента(); //AllowMainConfigurationObjectDeletion
	ЗаписьXML.ЗаписатьКонецЭлемента(); //Parameters	
	
	//Objects
	ЗаписьXML.ЗаписатьНачалоЭлемента("Objects");
	
	//Configuration
	ЗаписьXML.ЗаписатьНачалоЭлемента("Configuration");
	
	ДобавитьРежимОбъединенияНеОбъединять(ЗаписьXML);
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); //Configuration
	
	Для Каждого ИдентификаторМетаданных Из Массив Цикл
		
		Родитель 			= СоответсвиеДанныхИдентификаторовМетаданных[ИдентификаторМетаданных].Родитель;
		ПолноеИмяМетаданных = СоответсвиеДанныхИдентификаторовМетаданных[ИдентификаторМетаданных].ПолноеИмяМетаданных;
		ПолноеИмяМетаданных	= СтрЗаменить(ПолноеИмяМетаданных, "Общие.", "");
		
		Если ЗначениеЗаполнено(Родитель) Тогда
			
			//Object
			ЗаписьXML.ЗаписатьНачалоЭлемента("Object");
			ЗаписьXML.ЗаписатьАтрибут("fullNameInSecondConfiguration", ПолноеИмяМетаданных);
			
			ДобавитьРежимОбъединенияВзятьИзВторойКонфигурации(ЗаписьXML);
			
			ЗаписьXML.ЗаписатьКонецЭлемента(); //Object	
			
		КонецЕсли;

	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); //Objects	
	ЗаписьXML.ЗаписатьКонецЭлемента(); //Settings	
	ЗаписьXML.Закрыть();
	
	ДвоичныеДанные 	= Новый ДвоичныеДанные(ИмяВременногоФайла);
	Адрес			= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Возврат Адрес
	
КонецФункции

#КонецОбласти

#Область Служебные

Процедура ПроверитьВозможностьФормированияФайлаПоДанным(Данные, Отказ)
	
	Если Не Данные.Свойство("Источник")
		Или Не ЗначениеЗаполнено(Данные.Источник) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнено свойство ""Источник"". Формирование файла настроек не возможно",,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ИнициализироватьСтруктуруДанных() Экспорт
		
	Данные = Новый Структура();
	Данные.Вставить("Источник");
	
	Возврат Данные
	
КонецФункции

Функция ПолучитьМассивИдентификаторовОбъектовМетаданныхПоВыпускуРелиза(Источник)
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	узВыпускРелизаИзмененныеОбъекты.ИдентификаторОбъектаМетаданных КАК ИдентификаторОбъектаМетаданных
		|ИЗ
		|	Документ.узВыпускРелиза.ИзмененныеОбъекты КАК узВыпускРелизаИзмененныеОбъекты
		|ГДЕ
		|	узВыпускРелизаИзмененныеОбъекты.Ссылка = &Источник";
	
	Запрос.УстановитьПараметр("Источник", Источник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Массив.Добавить(ВыборкаДетальныеЗаписи.ИдентификаторОбъектаМетаданных);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Массив
	
КонецФункции

Процедура ДобавитьРежимОбъединенияВзятьИзВторойКонфигурации(ЗаписьXML)
	
	//MergeRule
	ЗаписьXML.ЗаписатьНачалоЭлемента("MergeRule");
	ЗаписьXML.ЗаписатьТекст("GetFromSecondConfiguration");
	ЗаписьXML.ЗаписатьКонецЭлемента(); //MergeRule	
	
КонецПроцедуры

Процедура ДобавитьРежимОбъединенияНеОбъединять(ЗаписьXML)
	
	//MergeRule
	ЗаписьXML.ЗаписатьНачалоЭлемента("MergeRule");
	ЗаписьXML.ЗаписатьТекст("DoNotMerge");
	ЗаписьXML.ЗаписатьКонецЭлемента(); //MergeRule	
	
КонецПроцедуры

#КонецОбласти
